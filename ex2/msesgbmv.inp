
read rtf  card name go_gb1p.top
read rtf  card append name top_all22_prot.inp
read para card name go_gb1p.param
read para card append name par_all22_prot.inp
read psf  card name gb1p_hybrid.psf
read coor card name gb1p_hybrid.crd

! NONBOND setup
! ------------------------------------------------------------------
NBOND atom switch cdie vdw vswitch -
      ctonnb 16.0 ctofnb 18.0 cutnb 20.0

! GBMV implicit solvent model 
! ------------------------------------------------------------------
scalar wmain = radii
set GBRAdiiname  = radii_c36gbmvop.str
stream @GBRAdiiname
scalar wmain set 0 sele segid CG end ! CG system does not include the GBMV2/SA calculations
GBMV BETA -12  P3 0.65 WATR 1.4  SHIFT -0.102 SLOPE 0.9085  P6 8 SA 0.015 -
     WTYP 2 nphi 38 KAPPA 0.0 weight

! Harmonic restrict
! -------------------------------------------------------------------
 cons harm clear
 cons harm force 5.0 sele segid AT .and. resid 2 .and. type CA end
 cons harm force 5.0 sele segid CG .and. resid 2 .and. type CA end

! BLOCK AT and CG system
! -------------------------------------------------------------------
block
    clear  ! clear any prevous block setup as a safte guard
end
block 2  ! requesting blocks
  call 1 select segid CG end  ! this is a special case
  call 2 select segid AT end  ! block actually does not work in MSES calculations
  coef 1 1 1.0                ! for most of cases. Before using, please make sure
  coef 2 2 1.0                ! the GPU block can get the same results of CPU block
  coef 1 2 0.0
end

! MSES setup
! -------------------------------------------------------------------
open read card unit 10 name contact.list
MSES scal 2.0 iunlist 10

! OMM setup
! -------------------------------------------------------------------
omm platform cuda

! GPU energy calculation
! -------------------------------------------------------------------
energy omm
coor force comp
write coor comp pdb name gpuforcebeforedyna.dat

! GPU dynamic calculation
! -------------------------------------------------------------------
set fbeta = 0.1
scalar fbeta set @fbeta
scalar fbeta set 0.0 select type H* end

shake  tolerance 1.0e-06 bonh param

set nsteps = 1000
set temp  = 300.0
dynamics leap start -
timestep 0.002 -      !!timestep interval
nstep @nsteps -       !!no. of steps
iseed 234234399 -     !!random seed for integrator
nprint 10 -           !!freq of printout
inbfrq -1 -
ilbfrq 0 -
tstruct @temp -
firstt @temp -
echeck -1  omm        !!energy tolerance check before crashing

energy 
coor force comp
write coor comp pdb name gpuforceafterdyna.dat

stop


